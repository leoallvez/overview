plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
}

def apiPropertiesFile = rootProject.file("api.properties")
def apiProperties = new Properties()
apiProperties.load(new FileInputStream(apiPropertiesFile))

android {
    compileSdk 31

    defaultConfig {
        applicationId "io.github.leoallvez.take"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField 'String', 'API_KEY', apiProperties['API_KEY']
        buildConfigField 'String', 'API_URL', '"https://api.themoviedb.org/3/"'
        buildConfigField 'String', 'IMG_URL', '"https://image.tmdb.org/t/p/w500"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        dev {
            dimension "version"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            resValue 'string', 'app_name', 'Take - Dev'
            buildConfigField 'boolean', 'ADS_ARE_VISIBLES', 'true'
        }
        free {
            dimension "version"
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
            resValue 'string', 'app_name', 'Take'
            buildConfigField 'boolean', 'ADS_ARE_VISIBLES', 'false'
        }
        paid {
            dimension "version"
            applicationIdSuffix ".paid"
            versionNameSuffix "-paid"
            resValue 'string', 'app_name', 'Take'
            buildConfigField 'boolean', 'ADS_ARE_VISIBLES', 'false'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion '1.5.21'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.0-alpha06'

    //Firebase Module
    implementation project(path: ':firebase')
    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'

    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"

    // Navigation
    implementation "androidx.navigation:navigation-compose:2.4.0-alpha06"
    // Hilt
    def hilt = '2.37'
    implementation "com.google.dagger:hilt-android:$hilt"
    kapt "com.google.dagger:hilt-android-compiler:$hilt"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0-alpha03"
    // Room
    def room = '2.4.0'
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    kapt "androidx.room:room-compiler:$room"
    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation "com.google.accompanist:accompanist-swiperefresh:0.21.4-beta"
    //Google Ads
    implementation 'com.google.android.gms:play-services-ads:20.6.0'

    // Paging Compose
    implementation "com.google.accompanist:accompanist-pager:0.13.0"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.13.0"

    // Third party library
    implementation 'com.jakewharton.timber:timber:5.0.1'
    def retrofit = '2.6.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation "com.github.haroldadmin:NetworkResponseAdapter:4.0.1"
    implementation 'io.coil-kt:coil-compose:2.0.0-rc03'
    implementation "me.onebone:toolbar-compose:2.3.3"

    testImplementation 'junit:junit:4.+'
    testImplementation 'io.mockk:mockk:1.12.3'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}
