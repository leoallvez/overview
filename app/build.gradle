plugins {
    id "com.android.application"
    id "kotlin-android"
    id "com.google.gms.google-services"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
    id "kotlin-parcelize"
    id "com.google.firebase.crashlytics"
}

def apiPropertiesFile = rootProject.file("api.properties")
def apiProperties = new Properties()
apiProperties.load(new FileInputStream(apiPropertiesFile))

android {
    compileSdk 33
    defaultConfig {
        applicationId "br.com.deepbyte.overview"
        minSdk 21
        targetSdk 33
        versionCode 2
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField 'String', 'API_KEY', apiProperties['API_KEY']
        buildConfigField 'String', 'API_URL', '"https://api.themoviedb.org/3/"'
        buildConfigField 'String', 'IMG_URL', '"https://image.tmdb.org/t/p/w780"'
        buildConfigField 'boolean', 'ADS_ARE_VISIBLES', 'true'
        resValue 'string', 'app_name', "Overview"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'DEBUG_BANNER_ID','""'
        }
        debug {
            buildConfigField 'String', 'DEBUG_BANNER_ID', '"ca-app-pub-3940256099942544/6300978111"'
        }
    }

    flavorDimensions "version"
    productFlavors {
        dev {
            resValue 'string', 'app_name', "Over-DEV"
            dimension "version"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            buildConfigField 'boolean', 'ADS_ARE_VISIBLES', 'true'
        }
        homol {
            resValue 'string', 'app_name', "Over-HOM"
            dimension "version"
            applicationIdSuffix ".homol"
            versionNameSuffix "-homol"
            buildConfigField 'boolean', 'ADS_ARE_VISIBLES', 'true'
        }
        prod {
            buildConfigField 'boolean', 'ADS_ARE_VISIBLES', 'true'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:1.3.1"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
    implementation "androidx.activity:activity-compose:1.7.0-alpha02"
    // Firebase Module
    implementation project(path: ':firebase')
    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.0-alpha03"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.1"
    // Navigation
    implementation "androidx.navigation:navigation-compose:2.6.0-alpha04"
    // Hilt
    def hilt = "2.44.2"
    implementation "com.google.dagger:hilt-android:$hilt"
    kapt "com.google.dagger:hilt-android-compiler:$hilt"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    // Room
    def room = "2.4.3"
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    kapt "androidx.room:room-compiler:$room"
    // Paging
    def paging_version = "3.1.1"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-compose:1.0.0-alpha17"
    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    // Google Ads
    implementation "com.google.android.gms:play-services-ads:21.4.0"
    // Accompanist
    def accompanist = "0.28.0"
    implementation "com.google.accompanist:accompanist-pager:$accompanist"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist"

    // Third party library
    implementation "com.jakewharton.timber:timber:5.0.1"
    def retrofit = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
    implementation "com.google.code.gson:gson:2.10.1"
    // noinspection GradleDependency
    implementation "com.github.haroldadmin:NetworkResponseAdapter:4.1.0"
    implementation "io.coil-kt:coil-compose:2.2.2"
    implementation "me.onebone:toolbar-compose:2.3.5"
    implementation "com.ehsanmsz:msz-progress-indicator:0.2.0"

    testImplementation "junit:junit:4.13.2"
    testImplementation "io.mockk:mockk:1.13.3"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    androidTestImplementation "androidx.test.ext:junit:1.1.4"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.4.0-alpha03"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}
