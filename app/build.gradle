plugins {
    id "com.android.application"
    id "kotlin-android"
    id "com.google.gms.google-services"
    id "dagger.hilt.android.plugin"
    id "kotlin-parcelize"
    id "com.google.firebase.crashlytics"
    id "kotlin-kapt"
    id 'com.google.devtools.ksp'
}

def activeSigning = System.getenv("ACTIVE_SIGNING") ? true : false
println("Is active signing: $activeSigning")

android {
    compileSdk 34
    defaultConfig {
        applicationId "br.com.deepbyte.overview"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        def api_key = System.getenv("API_KEY")
        buildConfigField 'String', 'API_KEY', api_key ? "\"$api_key\"" : '""'
        buildConfigField 'String', 'API_URL', '"https://api.themoviedb.org/3/"'
        buildConfigField 'String', 'IMG_URL', '"https://image.tmdb.org/t/p/w780"'
        buildConfigField 'boolean', 'ADS_ARE_VISIBLES', 'true'
        resConfigs "pt", "en", "es"
    }
    if (activeSigning) {
        signingConfigs {
            prod {
                storeFile rootProject.file(System.getenv("OVER_PROD_STORE_FILE"))
                storePassword System.getenv("OVER_PROD_PASSWORD")
                keyAlias System.getenv("OVER_PROD_KEY_ALIAS")
                keyPassword = System.getenv("OVER_PROD_PASSWORD")
            }
            homol {
                storeFile rootProject.file(System.getenv("OVER_HOMOL_STORE_FILE"))
                storePassword System.getenv("OVER_HOMOL_PASSWORD")
                keyAlias System.getenv("OVER_HOMOL_KEY_ALIAS")
                keyPassword = System.getenv("OVER_HOMOL_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            buildConfigField 'String', 'DEBUG_BANNER_ID','""'
        }
        debug {
            buildConfigField 'String', 'DEBUG_BANNER_ID', '"ca-app-pub-3940256099942544/6300978111"'
        }
    }

    flavorDimensions.add('version')
    productFlavors {
        dev {
            resValue "string", "app_name", "@string/app_name_dev"
            dimension "version"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }
        homol {
            resValue "string", "app_name", "@string/app_name_homol"
            dimension "version"
            applicationIdSuffix ".homol"
            versionNameSuffix "-homol"
            if (activeSigning) { signingConfig signingConfigs.homol }
        }
        prod {
            resValue "string", "app_name", "@string/app_name_prod"
            if (activeSigning) { signingConfig signingConfigs.prod }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerVersion kotlin_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
            unitTests.returnDefaultValues = true
        }
    }
    namespace 'br.com.deepbyte.overview'
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
}

dependencies {
    implementation platform('androidx.compose:compose-bom:2022.12.00')
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.compose.runtime:runtime-livedata"

    implementation "androidx.lifecycle:lifecycle-runtime"
    implementation "androidx.activity:activity-compose:1.8.0-alpha06"
    // Firebase Module
    implementation project(path: ':firebase')
    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
    // Navigation
    implementation "androidx.navigation:navigation-compose:2.7.0"
    // Hilt
    def hilt = "2.46.1"
    implementation "com.google.dagger:hilt-android:2.47"
    kapt "com.google.dagger:hilt-android-compiler:$hilt"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    // Room
    def room = "2.5.2"
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    ksp "androidx.room:room-compiler:$room"
    // Paging
    def paging_version = "3.2.0"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.paging:paging-compose:$paging_version"
    // WorkManager
    def work_version = "2.8.1"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    // Google Ads
    implementation "com.google.android.gms:play-services-ads:22.2.0"
    // Accompanist
    def accompanist = "0.30.1"
    implementation "com.google.accompanist:accompanist-pager:$accompanist"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist"

    // Third party library
    implementation "com.jakewharton.timber:timber:5.0.1"
    def retrofit = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:4.11.0"
    // noinspection GradleDependency
    implementation "com.github.haroldadmin:NetworkResponseAdapter:4.1.0"
    implementation "io.coil-kt:coil-compose:2.4.0"
    implementation "me.onebone:toolbar-compose:2.3.5"
    implementation "com.ehsanmsz:msz-progress-indicator:0.2.0"

    testImplementation "junit:junit:4.13.2"
    testImplementation "io.mockk:mockk:1.13.7"
    testImplementation 'org.amshove.kluent:kluent-android:1.73'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.6.0-alpha03"
    debugImplementation "androidx.compose.ui:ui-tooling"
}
